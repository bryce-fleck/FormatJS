load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin")
# Add rules here to build your software
# See https://docs.bazel.build/versions/master/build-ref.html#BUILD_files

# Allow any ts_library rules in this workspace to reference the config
# Note: if you move the tsconfig.json file to a subdirectory, you can add an alias() here instead
#   so that ts_library rules still use it by default.
#   See https://www.npmjs.com/package/@bazel/typescript#installation
exports_files(
    [
        "tsconfig.json",
        "tsconfig.esm.json",
        "tsconfig.es6.json",
        "api-extractor.json",
        "rollup.config.js",
        "karma.conf.js",
        "karma.conf-ci.js",
        "jest.config.js",
    ],
    visibility = ["//:__subpackages__"],
)

copy_to_bin(
    name = "setup-api-extractor",
    srcs = [
        "api-extractor.json",
        "tsconfig.json",
    ],
    visibility = ["//:__subpackages__"],
)

PLURALRULES_TESTS = glob(
    ["test262/test/intl402/PluralRules/**/*.js"],
    exclude = ["**/proto-from-ctor-realm.js"],
)

NUMBERFORMAT_TESTS = glob(
    ["test262/test/intl402/NumberFormat/**/*.js"],
    exclude = [
        "**/builtin.js",  # Built-in functions cannot have `prototype` property.
        "**/constructor-order.js",  # buggy test case. The spec states that impl should throw RangeError.
        "**/constructor-locales-hasproperty.js",  # This checks that we only iterate once...
        "**/constructor-unit.js",  # buggy test case. The spec states that impl should throw RangeError.
        "**/currency-digits.js",  # AFN's currency digits differ from CLDR data.
        "**/legacy-regexp-statics-not-modified.js",  # TODO
        "**/proto-from-ctor-realm.js",  # Bc of Realm support
        "**/constructor-locales-get-tostring.js",  # Bc our Intl.getCanonicalLocales isn't really spec-compliant
        "**/taint-Object-prototype.js",  # Bc our Intl.getCanonicalLocales isn't really spec-compliant
        "**/numbering-systems.js",  # See https://github.com/tc39/ecma402/pull/438
    ],
)

FILES = glob([
    "test262/**/*.js",
    "test262/**/*.json",
]) + ["test262/README.md"]

filegroup(
    name = "test262-pluralrules",
    srcs = PLURALRULES_TESTS,
    visibility = ["//packages/intl-pluralrules:__subpackages__"],
)

filegroup(
    name = "test262-numberformat",
    srcs = NUMBERFORMAT_TESTS,
    visibility = ["//packages/intl-numberformat:__subpackages__"],
)

filegroup(
    name = "test262-all",
    srcs = FILES,
    visibility = ["//visibility:public"],
)
